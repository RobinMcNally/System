typeset -U PATH # Prevents duplicate PATH entries. zsh only, so it's set prior to sourcing anything that might set PATH.

source $HOME/.profile

HISTFILE=$HOME/.zsh_history
setopt APPEND_HISTORY
HISTSIZE=1200
SAVEHIST=1000
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
#setopt EXTENDED_HISTORY
setopt HIST_IGNORE_SPACE
setopt PROMPT_SUBST

autoload -U colors && colors

set -o emacs # I just cannot get used to vim bindings on the shell, they're clunky. Disabling since EDITOR would set it.

# These force vim-style behavior, rather than vi-style. Screw vi.
#bindkey "^?" backward-delete-char
#bindkey "^W" backward-kill-word
#bindkey "^H" backward-delete-char
#bindkey "^U" kill-line

# Autocompletions
autoload -Uz compinit
compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' completions 1
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort modification
zstyle ':completion:*' format '%F{blue}Completing %d%f'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent ..
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '+' '+m:{[:lower:]}={[:upper:]}' '+r:|[._-]=** r:|=**' '+'
zstyle ':completion:*' max-errors 3
zstyle ':completion:*' menu select=0
zstyle ':completion:*' original false
zstyle ':completion:*' prompt 'Corrections (%e):'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' substitute 1
zstyle ':completion:*' verbose true

h=()
if [[ -r ~/.ssh/config ]]; then
    h=($h ${${${(@M)${(f)"$(cat ~/.ssh/config)"}:#Host *}#Host }:#*[*?]*})
fi
if [[ $#h -gt 0 ]]; then
    zstyle ':completion:*:ssh:*' hosts $h
    zstyle ':completion:*:slogin:*' hosts $h
fi

# General aliases
alias tmx='tmux new-session -A -s $USER'
alias src='source $HOME/.zshrc'
alias xcode='open *.xcworkspace || open *.xcodeproj' # Stolen from http://www.codeography.com/2009/10/28/open-xcode-project-from-the-command-line.html

# Colorful ls!
if [ $(uname) = "Linux" ]; then
    # Linux
    alias ls='ls -F --color=auto'
    export LS_COLORS='di=33:fi=0:ln=35:pi=34:so=36:bd=7;32:cd=32:or=37:mi=0:ex=31'
else
    # OS X   
    alias ls='ls -FG'
    export LSCOLORS=dxfxgxexbxxccxBxBxdADA
    export CLICOLOR=1
fi

# Functions

cdls () {
    if [[ -z $2 ]]; then
        cd $1 && ls
    else
        cd $2 && ls $1
    fi
}

count_git_items () {
    echo "$1" | awk "{c+=gsub(/$2/,\"\")} END{print c+0}"
}

build_prompt () {
    if $(git branch 1> /dev/null 2>&1); then
        # We're in a git repo, so activate the special prompt.
        git_name=$(git config user.name)
        repo_name=$(basename $(git rev-parse --show-toplevel))

        subpath=$(git rev-parse --show-prefix)
        [ -n "$subpath" ] && subpath="/$subpath" # Only display a slash if we're actually in a subdirectory.

        unstage_status=""
        stage_status=""

        git_status=$(git status --short --branch)

        branch_line=$(echo "$git_status" | grep "^##")
        branch=$(echo "$branch_line" | cut -f2- -d' ' | cut -f1 -d'.')
        drift=$(echo "$branch_line" | sed -n "s/.*\[.*ahead \(.*\).*\].*/→\1 /p")
        drift="$drift$(echo "$branch_line" | sed -n 's/.*\[.*behind \(.*\).*\].*/←\1 /p')"

        untracked=$(count_git_items $git_status "^\?\?")
        [ $untracked -ne 0 ] && unstage_status="$unstage_status %F{magenta}+$untracked"

        modified_unstaged=$(count_git_items $git_status "^.M")
        [ $modified_unstaged -ne 0 ] && unstage_status="$unstage_status %F{magenta}~$modified_unstaged"

        deleted_unstaged=$(count_git_items $git_status "^.D")
        [ $deleted_unstaged -ne 0 ] && unstage_status="$unstage_status %F{magenta}-$deleted_unstaged"
       
        added_staged=$(count_git_items $git_status "^A")
        [ $added_staged -ne 0 ] && stage_status="$stage_status %F{green}+$added_staged"

        modified_staged=$(count_git_items $git_status "^[MR]")
        [ $modified_staged -ne 0 ] && stage_status="$stage_status %F{yellow}~$modified_staged"

        deleted_staged=$(count_git_items $git_status "^D")
        [ $deleted_staged -ne 0 ] && stage_status="$stage_status %F{red}-$deleted_staged"

        divider=""
        [[ -n $unstage_status ]] && [[ -n $stage_status ]] && divider="%B%F{black} //%f%b"
        [[ -z $unstage_status ]] && [[ -z $stage_status ]] && divider="%B%F{black} ✓%f%b"

        branch="%F{blue}$branch"

        PROMPT="%B%F{black}$git_name ± %f%b%F{yellow}$repo_name%f%B%F{black}$subpath ⋋ %b$branch $drift%B%F{black}[%b$unstage_status$divider$stage_status %B%F{black}]%b
%(#.%B%F{red}.%F{blue})>> %f%b"
    else
        PROMPT="%B%(#.%F{red}ROOT%f.%F{black}%n)%F{black} @ %m : %f%b%F{yellow}%d%f %1(j.%F{cyan}⎇  %j%f.)
%(#.%B%F{red}.%F{blue})>> %f%b"
    fi
}

precmd () {
    build_prompt
}

# Right-hand prompt is always the same:

# Display the exit status of the last command as a green/red status light, and
# print out the code if it's > 0.
RPROMPT="%(?.%F{green}.%F{red}%? )%{%G•%}%f"

# Finally, stuff to run before the first prompt:

homebrew_updates () {
    # Calls `brew update` if it's been a day since the last check, then prints any
    # `brew outdated` packages in a nice format.

    which brew 2&>1 > /dev/null || return # Make sure homebrew's around.

    homebrew_epoch=$(date -jf "%Y-%m-%d" $(brew --version | sed -E 's/.* ([0-9-]+)\)/\1/') "+%s")
    now=$(date -j "+%s")

    if [[ $[ (now - homebrew_epoch) ] -ne 0 ]]; then
        echo -e "\x1b[33mUpdating Homebrew, this may take a bit…\x1b[0m"
        brew update 2&>1 > /dev/null || echo "\x1b[31mCouldn't update Homebrew.\x1b[0m"
    fi

    local IFS='
'
    outdated=( $(brew outdated --verbose | sed -E 's/([a-zA-Z-]+) \(.* ([0-9.]+)\)/\\x1b[35m\1\\x1b[0m \2/') )

    if [[ -n "$outdated" ]]; then
        separator=", "
        outdated="$( printf "${separator}%s" "${outdated[@]}" )"
        outdated="${outdated:${#separator}}" # remove leading separator

        echo "\x1b[32;1mUpdates available\x1b[0m in Homebrew:"
        echo -e "    ${outdated[@]}"
    fi
}

homebrew_updates
