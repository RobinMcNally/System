typeset -U PATH # Prevents duplicate PATH entries. zsh only, so it's set prior to sourcing anything that might set PATH.

source $HOME/.profile

HISTFILE=$HOME/.zsh_history
setopt APPEND_HISTORY
HISTSIZE=1200
SAVEHIST=1000
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
#setopt EXTENDED_HISTORY

autoload -U colors && colors

# vim mode
set -o vi # This is already achieved by exporting EDITOR=vim, but best to be explicit.

# These force vim-style behavior, rather than vi-style. Screw vi.
bindkey "^?" backward-delete-char
bindkey "^W" backward-kill-word
bindkey "^H" backward-delete-char
bindkey "^U" kill-line

PROMPT="%B%(#.%F{red}ROOT%f.%F{black}%n)%F{black} @ %m : %f%b%F{yellow}%d%f %1(j.%F{cyan}⎇  %j%f.)
%(#.%B%F{red}.%F{blue})>> %f%b"

# Display the exit status of the last command as a green/red status light, and
# print out the code if it's > 0.
RPROMPT="%(?.%F{green}.%F{red}%? )%{%G•%}%f"

# Autocompletions
autoload -Uz compinit
compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' completions 1
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort modification
zstyle ':completion:*' format '%F{blue}Completing %d%f'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent ..
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '+' '+m:{[:lower:]}={[:upper:]}' '+r:|[._-]=** r:|=**' '+'
zstyle ':completion:*' max-errors 3
zstyle ':completion:*' menu select=0
zstyle ':completion:*' original false
zstyle ':completion:*' prompt 'Corrections (%e):'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' substitute 1
zstyle ':completion:*' verbose true

h=()
if [[ -r ~/.ssh/config ]]; then
    h=($h ${${${(@M)${(f)"$(cat ~/.ssh/config)"}:#Host *}#Host }:#*[*?]*})
fi
if [[ $#h -gt 0 ]]; then
    zstyle ':completion:*:ssh:*' hosts $h
    zstyle ':completion:*:slogin:*' hosts $h
fi

# Aliases
alias tmx='tmux attach-session -t $USER || tmux new-session -s $USER'
alias fsl='fossil-colorized'
alias ffsl='fossil-overview'
alias src='source $HOME/.zshrc'

# Colorful ls!
if [ $(uname) = "Linux" ]; then
    # Linux
    alias ls='ls -F --color=auto'
    export LS_COLORS='di=33:fi=0:ln=35:pi=34:so=36:bd=32;47:cd=32:or=37:mi=0:ex=31'
else
    # OS X   
    alias ls='ls -FG'
    export LSCOLORS=dxfxgxexbxchcxBxBxdhDh
    export CLICOLOR=1
fi

# Functions

function chpwd() {
    fossil status > /dev/null 2>&1
    if [ "$?" -eq 0 ]; then
        fossil-overview
    fi
}

cdls () {
    if [[ -z $2 ]]; then
        cd $1 && ls
    else
        cd $2 && ls $1
    fi
}
